package SW
public

	with Data_Types;
	with CASE_Properties;
	with CASE_Scheduling;
	renames AGREE_Definitions::all;

	thread AttestationTester
		features
			AttestationRequest: in event data port Data_Types::AttestationRequestMsg.Impl;
			AttestationResponse: out event data port Data_Types::AttestationResponseMsg.Impl;
		properties
			CASE_Properties::Attesting => 100;
	end AttestationTester;

	thread implementation AttestationTester.Impl
		properties
			Dispatch_Protocol => Periodic;
			Period => 500ms;
			Compute_Execution_Time => 10ms .. 50ms;
			Stack_size => 1048576 Bytes;
			Source_Text => ("Component_Source/SW_AttestationTester/Pass/user_am.S");
			CASE_Properties::Component_Language => CakeML;
	end AttestationTester.Impl;

	process AttestationTester_seL4
		features
			AttestationRequest: in event data port Data_Types::AttestationRequestMsg.Impl;
			AttestationResponse: out event data port Data_Types::AttestationResponseMsg.Impl;
		properties
			CASE_Properties::Attesting => 100;
	end AttestationTester_seL4;

	process implementation AttestationTester_seL4.Impl
		subcomponents
			AttestationTester: thread AttestationTester.Impl;
		connections
			c1: port AttestationRequest -> AttestationTester.AttestationRequest;
			c2: port AttestationTester.AttestationResponse -> AttestationResponse;
		properties
			Period => 500ms;
			CASE_Properties::Component_Language => CakeML;
			CASE_Scheduling::Domain => 2;
	end AttestationTester_seL4.Impl;

	thread RadioDriver
		-- The RadioDriver is the software that enables other SW components to communicate to other systems over RF
		features
			MissionCommand: out event data port Data_Types::RF_Msg.Impl;
		properties
			CASE_Properties::Comm_Driver => true;
	end RadioDriver;

	thread implementation RadioDriver.Impl
		properties
			Dispatch_Protocol => Periodic;
			Period => 500ms;
			Compute_Execution_Time => 10ms .. 50ms;
			Stack_size => 1048576 Bytes;
			CASE_Scheduling::Domain => 3;
	end RadioDriver.Impl;

	thread FlightPlanner
		-- The FlightPlanner accepts a command message containing a map and flight pattern, and generates a mission.
		features
			MissionCommand: in event data port Data_Types::RF_Msg.Impl;
			FlightPlan: out event data port Data_Types::Mission;
		annex agree {**
			assume Req_Filter "Messages shall be well-formed" : event(MissionCommand) => WELL_FORMED_MISSION_COMMAND(MissionCommand);
		**};
	end FlightPlanner;

	thread implementation FlightPlanner.Impl
		properties
			CASE_Properties::Trust_Level => 0;
			Dispatch_Protocol => Periodic;
			Period => 500ms;
			Compute_Execution_Time => 10ms .. 50ms;
			Stack_size => 1048576 Bytes;
			CASE_Scheduling::Domain => 7;
	end FlightPlanner.Impl;

	thread FlightController
		-- The FlightController is the software that flies the UAV
		features
			Alert: in event port;
			FlightPlan: in event data port Data_Types::Mission;
	end FlightController;

	thread implementation FlightController.Impl
		properties
			Dispatch_Protocol => Periodic;
			Period => 500ms;
			Compute_Execution_Time => 10ms .. 50ms;
			Stack_size => 1048576 Bytes;
			CASE_Scheduling::Domain => 9;
	end FlightController.Impl;

	process SW
	end SW;

	process implementation SW.Impl
		subcomponents
			RadioDriver: thread RadioDriver.Impl;
			FlightPlanner: thread FlightPlanner.Impl;
			FlightController: thread FlightController.Impl;
		connections
			c1: port RadioDriver.MissionCommand -> FlightPlanner.MissionCommand;
			c2: port FlightPlanner.FlightPlan -> FlightController.FlightPlan;
	end SW.Impl;

	process RadioDriver_seL4
		features
			MissionCommand: out event data port Data_Types::RF_Msg.Impl;
		properties
			CASE_Properties::Comm_Driver => true;
	end RadioDriver_seL4;

	process implementation RadioDriver_seL4.Impl
		subcomponents
			RadioDriver: thread RadioDriver.Impl;
		connections
			c1: port RadioDriver.MissionCommand -> MissionCommand;
		properties
			Period => 500ms;
			CASE_Scheduling::Domain => 3;
	end RadioDriver_seL4.Impl;

	process FlightPlanner_seL4
		features
			MissionCommand: in event data port Data_Types::RF_Msg.Impl;
			FlightPlan: out event data port Data_Types::Mission;
	end FlightPlanner_seL4;

	process implementation FlightPlanner_seL4.Impl
		subcomponents
			FlightPlanner: thread FlightPlanner.Impl;
		connections
			c1: port MissionCommand -> FlightPlanner.MissionCommand;
			c2: port FlightPlanner.FlightPlan -> FlightPlan;
		properties
			CASE_Properties::Trust_Level => 0;
			Period => 500ms;
			CASE_Scheduling::Domain => 7;
		annex agree {**
			lift contract ;
		**};
	end FlightPlanner_seL4.Impl;

	process FlightController_seL4
		features
			Alert: in event port;
			FlightPlan: in event data port Data_Types::Mission;
	end FlightController_seL4;

	process implementation FlightController_seL4.Impl
		subcomponents
			FlightController: thread FlightController.Impl;
		connections
			c1: port FlightPlan -> FlightController.FlightPlan;
			c2: port Alert -> FlightController.Alert;
		properties
			Period => 500ms;
			CASE_Scheduling::Domain => 9;
	end FlightController_seL4.Impl;

	system SW_seL4
	end SW_seL4;

	system implementation SW_seL4.Impl
		subcomponents
			RadioDriver: process RadioDriver_seL4.Impl;
			FlightPlanner: process FlightPlanner_seL4.Impl;
			FlightController: process FlightController_seL4.Impl;
		connections
			c1: port RadioDriver.MissionCommand -> FlightPlanner.MissionCommand;
			c2: port FlightPlanner.FlightPlan -> FlightController.FlightPlan;
	end SW_seL4.Impl;
end SW;